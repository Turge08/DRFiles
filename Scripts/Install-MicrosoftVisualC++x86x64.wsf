<job id="Install-MicrosoftVisualC++x86x64">
   <script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
   <script language="VBScript"> 
      Option Explicit 
      Dim iRetVal 

      iRetVal = ZTIProcess
      ProcessResults iRetVal
      On Error Goto 0 

      '//---------------------------------------------------------------------------
      '// Function: ZTIProcess()
      '//---------------------------------------------------------------------------
      Function ZTIProcess() 
         '// Specifies the versions to install
         Dim Versions : Versions = Array("2008", "2010", "2012", "2013", "2015", "2017")
         Dim SourceFolder : SourceFolder = "Source"
         Dim InstallArgument : InstallArgument = "/Q"
         Dim Version, Prefix, SetupFilename, Architecture

         oLogging.CreateEntry oUtility.ScriptName & ": Starting installation", LogTypeInfo

         '// Disable Zone Checks
         oEnv("SEE_MASK_NOZONECHECKS") = 1 		
         '// Note: You need to create the below folder structure and downloading the various redistributable packages from Microsoft
         '// https://support.microsoft.com/en-us/help/2977003/the-latest-supported-visual-c-downloads

         '// Source\VC2008\vcredist_x86.exe
         '// Source\VC2008\vcredist_x86.exe
         '// ...
         '// Source\VC2017\vc_redist.x86.exe
         '// Source\VC2017\vc_redist.x64.exe
         
         '// Determines the filename based on the version
         For Each Version In Versions
            Select Case Version
               Case "2008", "2010", "2012", "2013":
                  Prefix = "vcredist_"
               Case Else:
                  Prefix = "vc_redist."
            End Select
            '// Installs x64 version on 64-bit OS only
            For Each Architecture In Array("x86", "x64")
               If Architecture = "x86" Or oEnvironment.Item("Architecture") = "X64" Then
                  SetupFilename = SourceFolder & "\VC" & Version & "\" & Prefix & Architecture & ".exe"
                  If oFSO.FileExists(oUtility.ScriptDir & "\" & SetupFilename) Then
                     oLogging.CreateEntry oUtility.ScriptName & ": About to install " & SetupFilename, LogTypeInfo
                     iRetVal = oUtility.RunWithHeartbeat(chr(34) & oUtility.ScriptDir & "\" & SetupFilename & chr(34) & " " & InstallArgument)

                     If iRetVal = 0 or iRetVal = 3010 Then
                        ZTIProcess = Success 
                     Else 
                        ZTIProcess = Failure
                     End If
                     
                     oLogging.CreateEntry oUtility.ScriptName & ": Return code from command = " & iRetVal, LogTypeInfo
                     oLogging.CreateEntry oUtility.ScriptName & ": Finished installation", LogTypeInfo
                  Else
                     oLogging.CreateEntry oUtility.ScriptName & ": " & SetupFilename & " was not found, unable to install", LogTypeError
                  End If
               End If
            Next
         Next
         '/// Enable Zone Checks
         oEnv.Remove("SEE_MASK_NOZONECHECKS")
      End Function
   </script>
</job>
